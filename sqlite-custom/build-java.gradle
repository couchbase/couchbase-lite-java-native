apply plugin: 'cpp'
apply plugin: 'java'
apply plugin: 'maven'

version = System.getenv("MAVEN_UPLOAD_VERSION")

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

////////////////////////////////////////////
// For Native Library
////////////////////////////////////////////

model {
    platforms {
        osx_x86 {
            architecture "x86"
            operatingSystem "osx"
        }
        osx_x86_64 {
            architecture "x86_64"
            operatingSystem "osx"
        }
        linux_x86 {
            architecture "x86"
            operatingSystem "linux"
        }
        linux_x86_64 {
            architecture "x86_64"
            operatingSystem "linux"
        }
        linux_amd64 {
            architecture "amd64"
            operatingSystem "linux"
        }
        windows_x86 {
            architecture "x86"
            operatingSystem "windows"
        }
        windows_x86_64 {
            architecture "x86_64"
            operatingSystem "windows"
        }
        windows_amd64 {
            architecture "amd64"
            operatingSystem "windows"
        }
    }

    toolChains {
        visualCpp(VisualCpp)
        gcc(Gcc)
        clang(Clang)
    }

    repositories {
        libs(PrebuiltLibraries) {
            libsqlite3 {
                headers.srcDir "../vendor/sqlite/src/headers"
                binaries.withType(SharedLibraryBinary) {
                    def platformName = targetPlatform.name
                    def os = targetPlatform.operatingSystem.name;
                    def arch = targetPlatform.architecture.name.replaceAll("-", "_")
                    if (platformName == "windows_amd64" || platformName == "linux_amd64") {
                        arch = "amd64"
                    }
                    def libName = os == 'windows' ? "sqlite3.lib" : (os == 'osx' ? "libsqlite3.dylib" : "libsqlite3.so")
                    def libPath;
                    if (os == "osx") {
                        libPath = "../vendor/sqlite/libs/${os}/${libName}"
                    } else {
                        libPath = "../vendor/sqlite/libs/${os}/${arch}/${libName}"
                    }
                    sharedLibraryFile = file(libPath)
                }
            }
            libicuuc{
                headers.srcDir "../vendor/icu4c-android/include/common"
                headers.srcDir "../vendor/icu4c-android/include/i18n"
                binaries.withType(StaticLibraryBinary) {
                    def platformName = targetPlatform.name
                    def os = targetPlatform.operatingSystem;
                    def osName = os.name
                    def arch = targetPlatform.architecture.name.replaceAll("-", "_")
                    if (platformName == "windows_amd64" || platformName == "linux_amd64") {
                        arch = "amd64"
                    }
                    def libName = os.windows ? "sicuuc.lib" : "libicuuc.a"
                    def libPath = "../vendor/icu4c-android/libs/${osName}/${arch}/${libName}"
                    staticLibraryFile = file(libPath)
                }            
            }
            libicui18n{
                headers.srcDir "../vendor/icu4c-android/include/common"
                headers.srcDir "../vendor/icu4c-android/include/i18n"
                binaries.withType(StaticLibraryBinary) {
                    def platformName = targetPlatform.name
                    def os = targetPlatform.operatingSystem;
                    def osName = os.name
                    def arch = targetPlatform.architecture.name.replaceAll("-", "_")
                    if (platformName == "windows_amd64" || platformName == "linux_amd64") {
                        arch = "amd64"
                    }
                    def libName = os.windows ? "sicuin.lib" : "libicui18n.a"
                    def libPath = "../vendor/icu4c-android/libs/${osName}/${arch}/${libName}"
                    staticLibraryFile = file(libPath)
                }            
            }
            libicudata{
                headers.srcDir "../vendor/icu4c-android/include/common"
                headers.srcDir "../vendor/icu4c-android/include/i18n"
                binaries.withType(StaticLibraryBinary) {
                    def platformName = targetPlatform.name
                    def os = targetPlatform.operatingSystem;
                    def osName = os.name
                    def arch = targetPlatform.architecture.name.replaceAll("-", "_")
                    if (platformName == "windows_amd64" || platformName == "linux_amd64") {
                        arch = "amd64"
                    }
                    def libName = os.windows ? "sicudt.lib" : "libicudata.a"
                    def libPath = "../vendor/icu4c-android/libs/${osName}/${arch}/${libName}"
                    staticLibraryFile = file(libPath)
                }            
            }
        }
    }

    components {
        cbljavasqlitecustom(NativeLibrarySpec) {
            targetPlatform "osx_x86"
            targetPlatform "osx_x86_64"
            targetPlatform "linux_x86"
            targetPlatform "linux_x86_64"
            targetPlatform "linux_amd64"
            targetPlatform "windows_x86"
            targetPlatform "windows_x86_64"
            targetPlatform "windows_amd64"
            sources {
                cpp {
                    source {
                        srcDir "../jni/source"
                        exclude "**/com_couchbase_lite_internal_database_security_Key.cpp"
                    }
                    exportedHeaders {
                        srcDir "../jni/headers"
                    }
                    lib library: 'libsqlite3', linkage: 'shared'
                    lib library: 'libicui18n', linkage: 'static'
                    lib library: 'libicuuc',   linkage: 'static'
                    lib library: 'libicudata', linkage: 'static'
                }
            }
            binaries.withType(StaticLibraryBinary) { binary ->
                buildable = false
            }
            binaries.all {
                cppCompiler.args '-DUSE_ICU4C_UNICODE_COMPARE -DU_STATIC_IMPLEMENTATION'
                if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                    linker.args '-framework', "JavaVM"
                } else if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                } else if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/win32"
                    cppCompiler.args "-I" + VS_2015_INCLUDE_DIR
                    if(targetPlatform.architecture.name == "x86") {
                        linker.args "/LIBPATH:" + VS_2015_LIB_DIR + "/x86"
                    }
                    else /*if(targetPlatform.architecture.name == "x86-64" || targetPlatform.architecture.name == "amd64")*/ {
                        linker.args "/LIBPATH:" + VS_2015_LIB_DIR + "/x64"
                    }
                }

                if (toolChain in Gcc) {
                    cppCompiler.args "-ffunction-sections", "-fdata-sections", "-fomit-frame-pointer"
                    linker.args "-Wl,--no-undefined"
                    linker.args "-lpthread", "-ldl"                 
                }
            }
        }
    }
}

jar.baseName = "couchbase-lite-java-sqlite-custom"

binaries.withType(SharedLibraryBinary) { binary ->
    if (!buildable) {
        return
    }

    def builderTask = binary.tasks
    def platformName = targetPlatform.name
    def os = targetPlatform.operatingSystem.name
    def arch = targetPlatform.architecture.name.replaceAll("-", "_")
    if (platformName == "windows_amd64" || platformName == "linux_amd64") {
        arch = "amd64"
    }

    // Postprocessor task for OSX:
    def binaryName = builderTask.outputFile.get(0).getName();
    def postprocessTask = "postprocess_${platformName}_${binaryName}"
    task "${postprocessTask}"(type: Exec, dependsOn: builderTask) {
        if (os == "osx") {
            commandLine "install_name_tool", "-change", "/usr/local/lib/libsqlcipher.dylib", "@loader_path/libsqlcipher.dylib", builderTask.outputFile.get(0)
        } else {
            commandLine "echo", "> no process"
        }
    }

    // Jar output shared binary:
    jar.into("native/${os}/${arch}") {
        from builderTask.outputFile
    }

    // Jar sqlite3 library:
    jar.into("native/${os}/${arch}") {
        def libPath;
        def libName = os == 'windows' ? "sqlite3.dll" : (os == 'osx' ? "libsqlite3.dylib" : "libsqlite3.so")
        if (os == "osx") {
            libPath = "../vendor/sqlite/libs/${os}/${libName}"
        } else {
            libPath = "../vendor/sqlite/libs/${os}/${arch}/${libName}"
        }
        from libPath
    }

    if (os == "osx") {
        jar.dependsOn "${postprocessTask}"
    } else {
        jar.dependsOn builderTask
    }
}

////////////////////////////////////////////
// For Java
////////////////////////////////////////////

sourceSets {
    test {
        java {
            srcDirs = ["src/androidTest/java"]
            include "**/*Test.java"
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

/*
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
*/

////////////////////////////////////////////
// For Maven
////////////////////////////////////////////

task createMavenDirectory(type: Exec) {
    ext {
        uploadUser = System.getenv("MAVEN_UPLOAD_USERNAME") + ":" + System.getenv("MAVEN_UPLOAD_PASSWORD")
        mkcolPath  = System.getenv("MAVEN_UPLOAD_REPO_URL") + "com/couchbase/lite/couchbase-lite-java-sqlite-custom/" + version + "/"
    }
    commandLine "curl", "--user", uploadUser, "-X", "MKCOL", mkcolPath
}

// this hack is only needed for apache mod_dav based Maven repo's like file.couchbase.com.  otherwise, skip it
createMavenDirectory.onlyIf { System.getenv("MAVEN_UPLOAD_REPO_URL").contains("files") }

// first create the directory, then do the upload
task uploadArchivesWrapper(dependsOn: createMavenDirectory) << {
    uploadArchives.execute()
}

// this will upload, but will not first create a directory (which is needed on some servers)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getenv("MAVEN_UPLOAD_REPO_URL")) {
                authentication(userName: System.getenv("MAVEN_UPLOAD_USERNAME"), password: System.getenv("MAVEN_UPLOAD_PASSWORD"))
            }
            pom.version = version
            pom.groupId = 'com.couchbase.lite'
            pom.artifactId = 'couchbase-lite-java-sqlite-custom'
            pom.project {
                licenses {
                    license {
                        name 'Couchbase Community Edition License Agreement'
                        url 'http://www.couchbase.com/agreement/community'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
