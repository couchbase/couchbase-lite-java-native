import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'
apply plugin: 'maven'

version = System.getenv("MAVEN_UPLOAD_VERSION")

repositories {
    jcenter()
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir file('src/main/libs')
        }
    }

    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        def ndkBuild = "$ndkDir/ndk-build"
        if(Os.isFamily(Os.FAMILY_WINDOWS))
            ndkBuild = "$ndkDir\\ndk-build.cmd"
        commandLine "$ndkBuild", "-C", "src/main"
    }

    task cleanNdkBuild(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        def ndkBuild = "$ndkDir/ndk-build"
        if(Os.isFamily(Os.FAMILY_WINDOWS))
            ndkBuild = "$ndkDir\\ndk-build.cmd"
        commandLine "$ndkBuild", "-C", "src/main", "clean"
    }
    clean.dependsOn 'cleanNdkBuild'

    // Define a task for ndk-build
    tasks.withType(JavaCompile) {
        task -> task.dependsOn ndkBuild
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.aar')) {
                def file = output.outputFile
                def fileName = "couchbase-lite-android-sqlite-custom.aar"
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

////////////////////////////////////////////
// For Maven
////////////////////////////////////////////

task createMavenDirectory(type: Exec) {
    ext {
        uploadUser = System.getenv("MAVEN_UPLOAD_USERNAME") + ":" + System.getenv("MAVEN_UPLOAD_PASSWORD")
        mkcolPath  = System.getenv("MAVEN_UPLOAD_REPO_URL") + "com/couchbase/lite/couchbase-lite-android-sqlite-custom/" + version + "/"
    }
    commandLine "curl", "--user", uploadUser, "-X", "MKCOL", mkcolPath
}

// this hack is only needed for apache mod_dav based Maven repo's like file.couchbase.com.  otherwise, skip it
createMavenDirectory.onlyIf { System.getenv("MAVEN_UPLOAD_REPO_URL").contains("files") }

// first create the directory, then do the upload
task uploadArchivesWrapper(dependsOn: createMavenDirectory) << {
    uploadArchives.execute()
}

// this will upload, but will not first create a directory (which is needed on some servers)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getenv("MAVEN_UPLOAD_REPO_URL")) {
                authentication(userName: System.getenv("MAVEN_UPLOAD_USERNAME"), password: System.getenv("MAVEN_UPLOAD_PASSWORD"))
            }
            pom.version = version
            pom.groupId = 'com.couchbase.lite'
            pom.artifactId = 'couchbase-lite-android-sqlite-custom'
            pom.project {
                name 'com.couchbase.lite:couchbase-lite-android-sqlite-custom'
                description 'This is a shared native SQLite library used for Couchbase Lite Java.'
                url 'http://developer.couchbase.com/mobile/'
                licenses {
                    license {
                        name 'Couchbase, Inc. Community Edition License Agreement'
                        url 'https://www.couchbase.com/binaries/content/assets/website/legal/ce-license-agreement.pdf'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'http://developer.couchbase.com/mobile/'
                }
                developers {
                    developer {
                        name 'Couchbase Mobile'
                        email 'mobile@couchbase.com'
                        organization 'Couchbase'
                        organizationUrl 'http://www.couchbase.com'
                    }
               }
            }
        }
    }
}
